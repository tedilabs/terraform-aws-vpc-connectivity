output "id" {
  description = "The ID of the service."
  value       = data.aws_vpclattice_service.this.id
}

output "arn" {
  description = "The ARN of the service."
  value       = data.aws_vpclattice_service.this.arn
}

output "name" {
  description = "The name of the service."
  value       = data.aws_vpclattice_service.this.name
}

output "status" {
  description = "The status of the service."
  value       = data.aws_vpclattice_service.this.status
}

output "auth_type" {
  description = "The type of authentication and authorization that manages client access to the service."
  value       = data.aws_vpclattice_service.this.auth_type
}

output "domain" {
  description = "The DNS name of the service."
  value       = one(data.aws_vpclattice_service.this.dns_entry[*].domain_name)
}

output "zone_id" {
  description = "The canonical hosted zone ID of the service to be used in a Route 53 CNAME record."
  value       = one(data.aws_vpclattice_service.this.dns_entry[*].hosted_zone_id)
}

output "custom_domain" {
  description = "The configuration for a custom domain name to use in addition to the domain name that is generated by VPC Lattice."
  value = {
    name            = data.aws_vpclattice_service.this.custom_domain_name
    tls_certificate = data.aws_vpclattice_service.this.certificate_arn
  }
}

output "service_network_associations" {
  description = <<EOF
  The configuration for the service network associations with the service.
    `id` - The ID of the association.
    `arn` - The ARN of the Association.
    `status` - The operations status. Valid Values are `CREATE_IN_PROGRESS`, `ACTIVE`, `DELETE_IN_PROGRESS`, `CREATE_FAILED`, `DELETE_FAILED`.
    `created_by` - The principal that created the association.

    `service_network` - The ARN (Amazon Resource Name) of the service network.
  EOF
  value = {
    for name, association in aws_vpclattice_service_network_service_association.this :
    name => {
      id         = association.id
      arn        = association.arn
      status     = association.status
      created_by = association.created_by

      service_network = association.service_network_identifier
    }
  }
}

output "resource_group" {
  description = "The resource group created to manage resources in this module."
  value = merge(
    {
      enabled = var.resource_group.enabled && var.module_tags_enabled
    },
    (var.resource_group.enabled && var.module_tags_enabled
      ? {
        arn  = module.resource_group[0].arn
        name = module.resource_group[0].name
      }
      : {}
    )
  )
}
